#version 460

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

// input sampler bindings
layout(binding = 0) uniform sampler2D u_PositionBuffer;
layout(binding = 1) uniform sampler2D u_NormalBuffer;
layout(binding = 2) uniform sampler2D u_AlbedoBuffer;

// probe data
struct DDGIProbe
{
    vec4 WorldPosition;
    vec4 Irradiance;
    float HitDistance;
};

// struct for storing debug ray info
struct DebugRay
{
    vec3 origin;
    vec3 hit;
    vec3 color;
};

layout (std430, binding = 3) buffer ProbeBuffer
{
    DDGIProbe probes[];
};

layout(std430, binding = 4) buffer DebugRayBuffer
{
    DebugRay debugRays[];
};

layout(std140, binding = 5) uniform ShaderGlobalData 
{
    mat4 viewMatrix;
    mat4 projMatrix;
    vec4 camPos;
    vec4 camDir;
    vec2 timeInfo;
    vec2 windowSize;
    int frameCount;
};

// uniforms
uniform int u_DebugRayCount;
uniform int u_DebugProbeIndex;
uniform ivec3 u_GridResolution;
uniform vec3 u_ProbeSpacing;
uniform vec3 u_GridOrigin;
uniform int u_RaysPerProbe;
uniform float u_BlendFactor;
uniform mat4 u_InverseView;
uniform float u_HitThreshold;

// ray marching constants
const int maxSteps = 64;
const float maxDistance = 7.0;
const float stepSize = maxDistance / float(maxSteps);
const float normalThreshold = 0.8; 
const float minStepScale = 0.5; // For adaptive step size
const float maxStepScale = 2.0; // For adaptive step size

// fast and effective way to distribute points 
// evenly (uniformly?) on a sphere
vec3 SampleFibonacciSphere(uint i, uint total)
{
    const float GOLDEN_RATIO = 1.61803398875;
    float u = float(i);
    float v = float(total);

    float phi = 2.0 * 3.14159265 * fract(u * GOLDEN_RATIO);
    float cosTheta = 1.0 - 2.0 * (u + 0.5) / v;
    float sinTheta = sqrt(1.0 - cosTheta * cosTheta);

    return vec3(cos(phi) * sinTheta, cosTheta, sin(phi) * sinTheta);
}

// raymarch using the gbuffer
bool RaymarchGBufferHit(
    vec3 origin,
    vec3 dir,
    out vec3 hitPos,
    out vec3 hitNormal,
    out vec3 hitAlbedo,
    out int stepsTaken) 
{
    float currentStepSize = stepSize;
    float t = 0.0;

    for (int step = 0; step < maxSteps; ++step)
    {
        t = float(step) * stepSize;
        vec3 samplePos = origin + dir * t;

        if (t > maxDistance)
            break;

        vec4 viewPos = viewMatrix * vec4(samplePos, 1.0);
        vec4 clipPos = projMatrix * viewPos;

        if (viewPos.z > 0.0) 
        { 
            t += currentStepSize;
            continue;
        }

        if (clipPos.w <= 0.0)
            continue;

        vec3 ndc = clipPos.xyz / clipPos.w;
        vec2 uv = ndc.xy * 0.5 + 0.5;
        
        if (any(lessThan(uv, vec2(0)))) continue;
        if (any(greaterThan(uv, vec2(1)))) continue;

        ivec2 pixel = ivec2(uv * windowSize);
        vec3 gBufferPos = texelFetch(u_PositionBuffer, pixel, 0).rgb;

        float expectedDepth = -viewPos.z;
        float actualDepth = -(viewMatrix * vec4(gBufferPos, 1.0)).z;
        float depthDiff = abs(actualDepth - expectedDepth);

        if (depthDiff < u_HitThreshold)
        {
            // additional normal consistency check
            vec3 surfaceNormal = normalize(mat3(u_InverseView) * texelFetch(u_NormalBuffer, pixel, 0).rgb);
            float normalSimilarity = clamp(dot(surfaceNormal, -dir), 0.0, 1.0);

            if (normalSimilarity > normalThreshold) 
            {
                hitPos = samplePos;
                hitNormal = surfaceNormal;
                hitAlbedo = texelFetch(u_AlbedoBuffer, pixel, 0).rgb;
                stepsTaken = step;
                return true;
            }
        }
        currentStepSize = clamp(stepSize * (1.0 + depthDiff * 2.0), 
        stepSize * minStepScale, 
        stepSize * maxStepScale);
        t += currentStepSize;
    }

    return false;
}

void ResetDebugRays(int index)
{
    for (int i = 0; i < u_RaysPerProbe; i++)
    {
        if (i < u_DebugRayCount && i < debugRays.length() && u_DebugProbeIndex == index)
        {
            debugRays[i].origin = vec3(0);
            debugRays[i].hit = vec3(0);
            debugRays[i].color = vec3(0);
        }
    }
}

void main()
{
    // index of the probe to update
    uint index = gl_GlobalInvocationID.x + 
                 gl_GlobalInvocationID.y * u_GridResolution.x + 
                 gl_GlobalInvocationID.z * u_GridResolution.x * u_GridResolution.y;

    // shouldn't occur, but just incase 
    if (index >= probes.length()) return;

    DDGIProbe probe = probes[index];

    if (index == u_DebugProbeIndex)
    {
        ResetDebugRays(u_DebugProbeIndex);
    }

    vec3 totalIrradiance = vec3(0.0);
    float totalDistance = 0.0;
    int hitCount = 0;

    for (int i = 0; i < u_RaysPerProbe; i++)
    {
        vec3 rayCol = vec3(0,0,1);
        vec3 origin = probe.WorldPosition.xyz;
        vec3 dir = SampleFibonacciSphere(i, u_RaysPerProbe);

        // setup defaults
        vec3 hitPos = origin + dir * maxDistance;
        vec3 hitAlbedo = vec3(1.0);
        vec3 hitNormal = vec3(0.0, 1.0, 0.0);

        int stepsTaken = 0;
        if (RaymarchGBufferHit(origin, dir, hitPos, hitNormal, hitAlbedo, stepsTaken))
        {
            vec3 incomingLight = vec3(1.0);
            vec3 irradiance = hitAlbedo * incomingLight * max(dot(-dir, hitNormal), 0.0);
            totalIrradiance += irradiance;
            totalDistance += length(hitPos - origin);
            hitCount++;
        }
        else // no hit
        {
            totalDistance += maxDistance;
            rayCol = vec3(1,0,0);
            hitPos = origin;
        }

        if (i < u_DebugRayCount && i < debugRays.length() && index == u_DebugProbeIndex)
        {
            debugRays[i].origin = origin;
            debugRays[i].hit = hitPos;
            // visualize the number of steps 
            debugRays[i].color = vec3(0, float(stepsTaken) / float(maxSteps), 0);
        }
    }

    // average out the accumulated irradiance and distance
    float hitRatio = float(hitCount) / float(u_RaysPerProbe);
    vec3 avgIrradiance = totalIrradiance / max(1, hitCount);
    float avgDistance = totalDistance / float(u_RaysPerProbe);

    // Apply blending with previous frame's data
    probe.Irradiance.rgb = mix(probe.Irradiance.rgb, avgIrradiance, u_BlendFactor * hitRatio);
    probe.HitDistance = mix(probe.HitDistance, avgDistance, u_BlendFactor);

    probes[index] = probe;
}